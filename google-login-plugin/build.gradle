// see https://github.com/JetBrains/gradle-intellij-plugin

def ideaLib = '../idea-IC/lib/'

dependencies {
    compile project(':common-lib')

    compile files('lib/google.gdt.eclipse.login.common.jar')
    compile files('lib/google-gct-login-context-ij-pg.jar')
    compile files('lib/google-api-services-oauth2-v2-rev70-1.18.0-rc.jar')
    compile files('lib/google-http-client-jackson-1.18.0-rc.jar')
    compile fileTree(dir: 'third_party/repackaged', include: '*.jar')
    // Where should we load these from? The versions bundled with IDEA contain
    // classes that are not in the versions loaded from Maven central; e.g. PlatformUtils
    compile files(ideaLib + 'annotations.jar')
    compile files(ideaLib + 'bootstrap.jar')
    compile files(ideaLib + 'extensions.jar')
    compile files(ideaLib + 'idea.jar')
    compile files(ideaLib + 'idea_rt.jar')
    compile files(ideaLib + 'jdom.jar')
    compile files(ideaLib + 'log4j.jar')
    compile files(ideaLib + 'openapi.jar')
    compile files(ideaLib + 'picocontainer.jar')
    compile files(ideaLib + 'trove4j.jar')
    compile files(ideaLib + 'util.jar')

    compile 'com.google.guava:guava:18.0'
    compile 'net.jcip:jcip-annotations:1.0'
    compile 'com.google.collections:google-collections:1.0'
    compile 'javax.servlet:servlet-api:2.5'

    testCompile 'junit:junit:4.+'
    testCompile 'org.mockito:mockito-all:1.9.5'
}

sourceSets {
    main {
        java {
            srcDirs 'src', 'resources'
        }
        resources {
            srcDir 'resources'
        }
    }
    test {
        java {
            srcDirs 'testSrc'
        }
    }
}

// because plugin.xml doesn't live in resources
jar {
    baseName = 'google-login'
    version = ''
    from ('./src') {
        include 'META-INF/plugin.xml'
    }
}

task plugin(type: Zip,  dependsOn: jar) {
    // by default uses version = 1.0
    baseName = 'google-login'
    destinationDir = file('build')
    include '*.jar'
    from('build/libs') {
        into('google-login/lib')
    }
    from('../common-lib/build/libs') {
        into('google-login/lib')
    }
    from('../common-lib/lib') {
        into('google-login/lib')
    }
    from('lib/') {
        into('google-login/lib')
    }
    from('third_party/repackaged') {
        into('google-login/lib')
    }
    from('third_party/javax-servlet-api') {
        into('google-login/lib')
    }
}

findbugs {
    ignoreFailures = true
    sourceSets = [sourceSets.main]
    effort = "max"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    ignoreFailures = true
    sourceSets = [sourceSets.main]
}

